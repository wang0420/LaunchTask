apply plugin: 'com.android.library'

//apply plugin: 'maven'
//apply plugin: 'com.novoda.bintray-release'

//添加两个插件
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def siteUrl = 'https://github.com/wang0420/LaunchTask'      // 项目的主页
def gitUrl = 'git@github.com:wang0420/LaunchTask.git'   // Git仓库的url
group = "com.github.wang"  // Maven Group ID for the artifact，一般填你唯一的包名
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
version = "1.1.0" //发布版本号
group = "com.github.wang"
//这个gruop很重要，参考 compile 'groupId:artifactId:version'     groupId就是这个，artifactId就是包名，version就是顶部那个

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName version

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "AndroidLib"//你要上传的库的名字
        name = "LaunchTask"    //发布到Bintray上的名字，这个是显示在bintray仓库中的项目名字，与包名可同可不同，若仓库没有这个项目文件夹，则会自动创建
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        userOrg = "personaldevelopment"
//自己创建的organization名称，一定要全是小写字母，即使当时创建的名称含有大写，这里全部转换为小写，否则404
        licenses = ['MIT']//不能随便写，只能是仓库创建时选择的license type
        publish = true// 是否是公开项目，公开别人可以引用
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Android 开源库'   //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'MIT' //和之前自己定义的协议一致
                        url 'https://raw.githubusercontent.com/minggo620/Pluto-Android/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'wangwei'        //填写的一些基本信息
                        name '王伟'
                        email '709693972@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false //必须添加以免出错
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
javadoc {
    options {
        //如果你的项目里面有中文注释的话，必须将格式设置为UTF-8，不然会出现乱码
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}




/*publish {
    userOrg = 'personaldevelopment'// 组织名，前面说了
    groupId = 'com.github.wang'// 包名（跟bintray里的package不是一回事）
    artifactId = 'LaunchTask'// library的名字
    publishVersion  = '1.0.0'// 版本 和前面两者组合起来就是依赖的内容compile 'com.xk:kinject:1.0.0'
    desc  = 'appLaunchTask库'
    website = "https://github.com/wang0420/ViewPagerIndicator"//这块我猜是随便写，可能是为了展示你这个项目的地址吧，写github上的地址即可
    repoName = 'appLaunchTask'//前面说了，repository name
}*/
